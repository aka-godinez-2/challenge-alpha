// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    apply from: './versions.gradle'

    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.0.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.20"
        classpath "gradle.plugin.com.github.konifar.gradle:plugin:0.3.3"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "com.osacky.doctor" version "0.7.3"
    id "org.jetbrains.kotlinx.kover" version "0.5.0"
    id "io.gitlab.arturbosch.detekt" version "1.19.0"
}

detekt {
    config = files("$rootDir/config/detekt/detekt.yml")
}

def projectSource = file(projectDir)
def configFile = files("$rootDir/config/detekt/detekt.yml")
def kotlinFiles = "**/*.kt"
def resourceFiles = "**/resources/**"
def buildFiles = "**/build/**"

tasks.register("detektAll", io.gitlab.arturbosch.detekt.Detekt) {
    def autoFix = project.hasProperty('detektAutoFix')

    description = "Custom DETEKT build for all modules"
    parallel = true
    ignoreFailures = false
    autoCorrect = autoFix
    buildUponDefaultConfig = true
    setSource(projectSource)
    config.setFrom(configFile)
    include(kotlinFiles)
    exclude(resourceFiles, buildFiles)
    reports {
        html.enabled = true
        xml.enabled = false
        txt.enabled = false
    }
}

// Desativando JAVA_HOME check para evitar muitas configurações para testar o projeto
doctor {
    javaHome {
        failOnError.set(false)
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
